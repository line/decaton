import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.0'
        classpath 'io.franzbecker:gradle-lombok:3.3.0'
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: "java-test-fixtures"
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'io.franzbecker.gradle-lombok'

    group = "com.linecorp.decaton"
    version = "${version}" + (snapshot.toBoolean() ? "-SNAPSHOT" : "")

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    ext {
        protobufVersion = "3.3.0"
        kafkaVersion = "2.4.0"
        prometheusVersion = "0.6.0"
        micrometerVersion = "1.5.1"
        lombokVersion = "1.18.4"
        junitVersion = "4.12"
        hamcrestVersion = "1.3"

        isReleaseVersion = !version.endsWith('-SNAPSHOT')
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'

    ext {
        shadeAllDependencies = false // Whether this project should results uber jar or not
        noPublish = false // Whether to publish this artifact to maven or not
        publishOnlyLocal = false // Whether to publish this artifact to local maven only (no effect if noPublish = true)
        relocatePb = true // Whether to relocate package name of protocol buffers classes
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"

        // Likely be used for most modules
        testImplementation "junit:junit:$junitVersion"
        testImplementation "org.mockito:mockito-core:2.23.4"
        testImplementation "org.mockito:mockito-inline:2.23.4"
    }

    configurations {
        shade
        compile.extendsFrom(shade)
        itCompile.extendsFrom(compile)
    }

    sourceSets.create('it') {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        compileClasspath += sourceSets.testFixtures.output
        runtimeClasspath += sourceSets.testFixtures.output
    }

    task integrationTest(type: Test) {
        testClassesDirs = sourceSets.it.output.classesDirs
        classpath = sourceSets.it.runtimeClasspath
    }

    tasks.withType(Test) {
        testLogging {
            // set options for log level LIFECYCLE
            events TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
            showExceptions true
            showCauses true
            showStackTraces true
            showStandardStreams false
        }
    }

    afterEvaluate {
        if (!noPublish) {
            publishing {
                repositories {
                    maven {
                        if (isReleaseVersion) {
                            url("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
                        } else {
                            url("https://oss.sonatype.org/content/repositories/snapshots/")
                        }
                        credentials {
                            username findProperty("sonatypeUsername")
                            password findProperty("sonatypePassword")
                        }
                    }
                }
                publications {
                    mavenJava(MavenPublication) {
                        afterEvaluate {
                            artifactId = rootProject.name + '-' + project.name

                            // Overwrite `artifacts` to clear previously added artifact XX-shadow.jar by `from components.shadow`
                            artifacts = [artifact(source: shadowJar, classifier: null), artifact(source: sourcesJar), artifact(source: javadocJar)]
                            // Rewrite all runtime dependencies to require them at compile time
                            pom.withXml {
                                def rootNode = asNode()
                                def depListNode = null
                                if (rootNode.dependencies) {
                                    depListNode = rootNode.dependencies.get(0)
                                } else {
                                    depListNode = new Node(rootNode, 'dependencies')
                                }

                                if (!shadeAllDependencies) {
                                    project.configurations.compile.allDependencies.each { dep ->
                                        if (!project.configurations.shade.allDependencies.contains(dep)) {
                                            def depNode = new Node(depListNode, 'dependency')
                                            depNode.appendNode('groupId', dep.group)
                                            def prefix = dep.group == 'com.linecorp.decaton' && !dep.name.startsWith('decaton-') ? 'decaton-' : ''
                                            depNode.appendNode('artifactId', prefix + dep.name)
                                            depNode.appendNode('version', dep.version)
                                            depNode.appendNode('scope', 'compile')
                                        }
                                    }
                                }
                            }
                            pom {
                                name = 'Decaton'
                                description = 'High throughput asynchronous task processing on Apache Kafka'
                                url = 'https://github.com/line/decaton'

                                scm {
                                    url = 'git@github.com:line/decaton.git'
                                    connection = 'scm:git:git@github.com:line/decaton.git'
                                    developerConnection = 'scm:git:git@github.com:line/decaton.git'
                                }

                                licenses {
                                    license {
                                        name = 'The Apache License, Version 2.0'
                                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                                    }
                                }

                                developers {
                                    developer {
                                        name = 'Decaton developers'
                                        email = 'dl_oss_dev@linecorp.com'
                                    }
                                }
                            }
                        }
                    }
                }
            }
            signing {
                required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
                sign publishing.publications.mavenJava
            }
        }

        shadowJar {
            if (!shadeAllDependencies) {
                // Shade only dependencies which are declared to be shaded explicitly.
                // By this line, all dependencies declared with `compile` or `runtime` will be added as
                // external dependencies and their classes aren't be merged into resulting shadow jar.
                configurations = [project.configurations.shade]
            }

            classifier = 'shadow'
            if (relocatePb) {
                // Relocate protobuf package prefixing unique identifier `com.linecorp.decaton`
                relocate "com.google.protobuf.", "com.linecorp.decaton.com.google.protobuf."
            }
        }

        tasks.withType(PublishToMavenRepository) {
            onlyIf {
                !publishOnlyLocal
            }
        }

        task sourcesJar(type: Jar, dependsOn: classes) {
            archiveClassifier = 'sources'
            from sourceSets.main.allSource
        }

        task delombok(type: io.franzbecker.gradle.lombok.task.DelombokTask, dependsOn: compileJava) {
            ext.outputDir = file("$buildDir/delombok")
            outputs.dir(outputDir)
            sourceSets.main.java.srcDirs.each {
                inputs.dir(it)
                args(it, "-d", outputDir)
            }
        }

        javadoc {
            dependsOn delombok
            source = delombok.outputDir
            options.encoding = 'UTF-8'
            options.locale = 'en_US'
        }

        task javadocJar(type: Jar) {
            archiveClassifier = 'javadoc'
            from javadoc
        }
        build.dependsOn(javadoc)
    }
}
