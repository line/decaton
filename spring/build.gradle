ext {
    boot2Version = "2.7.18"
    boot3Version = "3.1.10"
}

dependencies {
    api project(":processor")

    api "org.apache.kafka:kafka-clients:$kafkaVersion"
    api "org.springframework.boot:spring-boot-starter:$boot2Version"
}

// The following integrationTest profiles can be removed when support for boot2 is no longer needed.
sourceSets.create('integrationTestSpringBoot2') {
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
    compileClasspath += sourceSets.testFixtures.output
    runtimeClasspath += sourceSets.testFixtures.output
}

sourceSets.create('integrationTestSpringBoot3') {
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
    compileClasspath += sourceSets.testFixtures.output
    runtimeClasspath += sourceSets.testFixtures.output
}

task integrationTestSpringBoot2(type: Test) {
    testClassesDirs = sourceSets.integrationTestSpringBoot2.output.classesDirs
    classpath = sourceSets.integrationTestSpringBoot2.runtimeClasspath
}

task integrationTestSpringBoot3(type: Test) {
    testClassesDirs = sourceSets.integrationTestSpringBoot3.output.classesDirs
    classpath = sourceSets.integrationTestSpringBoot3.runtimeClasspath
}

check.dependsOn(integrationTestSpringBoot2, integrationTestSpringBoot3)

tasks.named('compileIntegrationTestSpringBoot3Java') {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    integrationTestSpringBoot2Implementation project(":processor")
    integrationTestSpringBoot2Implementation "org.springframework.boot:spring-boot-starter:$boot2Version"
    integrationTestSpringBoot2Implementation "org.springframework.boot:spring-boot-starter-test:$boot2Version"
    integrationTestSpringBoot2Implementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    integrationTestSpringBoot3Implementation project(":processor")
    integrationTestSpringBoot3Implementation "org.springframework.boot:spring-boot-starter:$boot3Version"
    integrationTestSpringBoot3Implementation "org.springframework.boot:spring-boot-starter-test:$boot3Version"
    integrationTestSpringBoot3Implementation "org.junit.jupiter:junit-jupiter:$junitVersion"
}
